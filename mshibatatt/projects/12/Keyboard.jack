// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Keyboard.jack

/**
 * A library for handling user input from the keyboard.
 */
class Keyboard {

    /** Initializes the keyboard. */
    function void init() {
        return;
    } 

    /**
     * Returns the character of the currently pressed key on the keyboard;
     * if no key is currently pressed, returns 0.
     *
     * Recognizes all ASCII characters, as well as the following keys:
     * new line = 128 = String.newline()
     * backspace = 129 = String.backspace()
     * left arrow = 130
     * up arrow = 131
     * right arrow = 132
     * down arrow = 133
     * home = 134
     * End = 135
     * page up = 136
     * page down = 137
     * insert = 138
     * delete = 139
     * ESC = 140
     * F1 - F12 = 141 - 152
     */
    function char keyPressed() {
        // memory[24576] is 0 if keys are not pressed
        return Memory.peek(24576);
    }

    /**								
     * Waits until a key is pressed on the keyboard and released,
     * then echoes the key to the screen, and returns the character 
     * of the pressed key.
     */
    function char readChar() {
        var char c;
        // show cursor
        let c = Keyboard.keyPressed();
        while (c = 0) {
            let c = Keyboard.keyPressed();
        }
        while (c = Keyboard.keyPressed()) {
            // keep wait
        }
        do Output.printChar(c);
        return c;
    }

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its value. Also handles user backspaces.
     */
    function String readLine(String message) {
        var String s;
        var char c;

        do Output.printString(message);
        let s = String.new(64); 
        while (true) {
            let c = Keyboard.readChar();
            if (c = 128) {
                do Output.println();
                return s;
            } else {
                if (c = 129) {
                    do s.eraseLastChar();
                } else {
                    do s.appendChar(c);
                }
            }
        }
        return s;
    }   

    /**								
     * Displays the message on the screen, reads from the keyboard the entered
     * text until a newline character is detected, echoes the text to the screen,
     * and returns its integer value (until the first non-digit character in the
     * entered text is detected). Also handles user backspaces. 
     */
    function int readInt(String message) {
        var int i;
        var char c;
        var boolean is_negative;

        do Output.printString(message);
        let i = 0;
        let is_negative = false;
        while (true) {
            let c = Keyboard.readChar();
            if (c = 129) {
                if ((i = 0) & is_negative) {
                    let is_negative = false;
                }
                let i = i / 10;
            } else {
                if (c = 45) {
                    if (i = 0) {
                        let is_negative = true;
                    }
                } else {
                    if ((c < 48) | (c > 57)) {
                        do Output.println();
                        return i;
                    } else {
                        if (is_negative) {
                            let i = (i*10) - (c-48);
                        } else {
                            let i = (i*10) + (c-48);
                        }
                    }
                }
            }
        }
        return i;
    }
}
